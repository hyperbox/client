apply plugin: 'base'

evaluationDependsOnChildren()

def apiPath = project.rootDir.toPath().resolve('..').resolve('api')
def binPath = project.buildDir.toPath().resolve('bin')
def binDir = 'bin'
def docDir = 'doc'
def iconDir = 'icons'
def libDir = 'lib'

def binCommonPath = binPath.resolve('common')
def binLinuxPath = binPath.resolve('linux')

task distBinJar(type: Copy) {
    dependsOn ':hbox-client-api:install'
    dependsOn subprojects.jar

    from project('hbox-client-gui').jar
    from project('hbox-client-core').jar
    into binCommonPath.resolve(binDir)
}

task distBinLib(type: Copy) {
    from subprojects.configurations.compile
    into binCommonPath.resolve(libDir)
}

task distBinDoc(type: Copy) {
    from apiPath.resolve(docDir).toFile()
    into binCommonPath.resolve(docDir)
}

task distBinIcons(type: Copy) {
    from project('hbox-client-gui').file(iconDir)
    from apiPath.resolve('media/img').toFile()
    into binCommonPath.resolve(iconDir)
}

task distBinCommon {
    dependsOn distBinJar, distBinLib, distBinDoc, distBinIcons
}

task distBinLinux(type: Copy) {
    dependsOn distBinCommon

    from project.file('scripts/hyperbox')
    from binCommonPath.toString() // using a Path creates a Stack Overflow
    into binLinuxPath
}
