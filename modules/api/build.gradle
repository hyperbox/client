apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

String gitVersion() {
    ByteArrayOutputStream out = new ByteArrayOutputStream()
    exec {
        commandLine = [ 'git', 'describe', '--always', '--dirty' ]
        standardOutput = out
    }
    return out.toString().replace(System.lineSeparator(), '')
}

group = 'io.kamax'
version = gitVersion()

sourceCompatibility = 1.7
targetCompatibility = 1.8

configurations {
    deployerJars
}

repositories {
    maven { url "https://maven.kamax.io/releases/" }
    maven { url "https://maven.kamax.io/snapshots/" }
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'io.kamax:hbox-api:0.0.14-2-ga95be4c'

    testCompile 'junit:junit:4.12'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:3.3.3'
}

uploadArchives {
    gradle.ext.mavenUrl = System.env.MAVEN_URL + (project.version ==~ /\d+\.\d+\.\d+/ ? '/releases' : '/snapshots')
    gradle.ext.mavenUser = System.env.MAVEN_USER
    gradle.ext.mavenPassword = System.env.MAVEN_PASS
    gradle.ext.mavenPrivateKey = System.env.MAVEN_PK

    doLast {
        println "Published ${project.version} to maven repository ${gradle.mavenUrl} with user ${gradle.mavenUser}"
    }

    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        repository(url: gradle.mavenUrl) {
            authentication(
                    userName: gradle.mavenUser,
                    password: gradle.mavenPassword,
                    privateKey: gradle.mavenPrivateKey
            )
        }
    }
}
